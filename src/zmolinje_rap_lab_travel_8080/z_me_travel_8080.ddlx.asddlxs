@Metadata.layer: #CORE
// Utilizamos la capa mas baja CORE

@UI: { headerInfo: { typeName: 'Travel',
                     typeNamePlural: 'Travels',
                     title: {
                        type: #STANDARD,
                        value: 'TravelID' } },
        presentationVariant: [{ sortOrder: [{ by: 'TravelID',
                                              direction: #DESC }],
                                visualizations: [{ type: #AS_LINEITEM }] }] }

//@UI.createHidden: true

annotate entity Z_C_TRAVEL_8080
    with 
{

// @UI.facet permite que cada item del listado tenga Navegación,
// al final de cada item apareceria un signo asi: >
 @UI.facet: [{ id: 'Travel',
                purpose: #STANDARD,
                type: #IDENTIFICATION_REFERENCE,
                label: 'Travel',
                position: 10 },
// Pestañas dentro de la Mavegación
               { id: 'Booking',
                purpose: #STANDARD,
                type: #LINEITEM_REFERENCE,
                label: 'Booking',
                position: 20,
                targetElement: '_Booking' }]
  @UI.hidden: true
  TravelUUID;

// Botones Aceptar , Rechazar, Desceunto
  @UI: { lineItem: [{ position: 10, importance: #HIGH },
                    { type: #FOR_ACTION,
                      dataAction: 'acceptTravel',
                      label: 'Accept Travel'},
                    { type: #FOR_ACTION,
                      dataAction: 'rejectTravel',
                      label: 'Reject Travel'},
                    { type: #FOR_ACTION,
                      dataAction: 'deductDiscount',
                      label: 'Discount'}],
          identification: [{ position: 10, label: 'Travel ID' },
                                { type: #FOR_ACTION,
                                  dataAction: 'acceptTravel',
                                  label: 'Accept Travel'},
                                { type: #FOR_ACTION,
                                  dataAction: 'rejectTravel',
                                  label: 'Reject Travel'},
                                { type: #FOR_ACTION,
                                  dataAction: 'deductDiscount',
                                  label: 'Discount'}],
          selectionField: [{ position: 10 }] }
  TravelID;

  @UI: { lineItem: [{ position: 20, importance: #HIGH }],
         identification: [{ position: 20 }],
         selectionField: [{ position: 20 }]}
  AgencyID;

  @UI: { lineItem: [{ position: 30, importance: #HIGH }],
         identification: [{ position: 30 }],
         selectionField: [{ position: 30 }]}
  CustomerID;

  @UI: { lineItem: [{ position: 40, importance: #MEDIUM }],
         identification: [{ position: 40 }]}
  BeginDate;

  @UI: { lineItem: [{ position: 50, importance: #MEDIUM }],
         identification: [{ position: 50 }]}
  EndDate;

  @UI: { lineItem: [{ position: 60, importance: #MEDIUM }],
         identification: [{ position: 60 }]}
  BookingFee;

  @UI: { lineItem: [{ position: 70, importance: #HIGH }],
        identification: [{ position: 70 }]}
  TotalPrice;
  

  @UI: { lineItem: [{ position: 80, importance: #MEDIUM }],
          identification: [{ position: 80 }]}
  Description;

  @UI: { lineItem: [{ position: 90, importance: #MEDIUM }],
         identification: [{ position: 90, label: 'Status' }],
         selectionField: [{ position: 40 }],
         textArrangement: #TEXT_ONLY }
  OverallStatus;

  @UI.hidden: true
  OverallStatusText;

  @UI.hidden: true
  LocalCreatedBy;
  @UI.hidden: true
  LocalCreatedAt;
  @UI.hidden: true
  LocalLastChangedBy;
  @UI.hidden: true
  LocalLastChangedAt;
  @UI.hidden: true
  LastChangedAt;
    
}